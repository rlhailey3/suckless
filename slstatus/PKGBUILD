pkgname=slstatus-hailey
_pkgname=slstatus
pkgver='beta'
pkgrel=1
pkgdesc="a status monitor for window managers that use WM_NAME or stdin to fill the status bar."
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11')
makedepends=('git')
provides=('slstatus')
conflicts=('slstatus')
url="http://tools.suckless.org/slstatus"
source=("git://git.suckless.org/slstatus"
        config.h)
md5sums=('SKIP'
         'SKIP') # so you can customize config.h
prepare() {
  cd $_pkgname
  if [[ -f "$srcdir/config.h" ]]; then
    cp -fv "$srcdir/config.h" config.h
  fi
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
